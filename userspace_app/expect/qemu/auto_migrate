#!/usr/bin/expect
# expect -f qemu_guest.sh command_file user passwd

set has_workload 1
set use_postcopy 1
set use_compress 0
set use_multifd 1
set use_xbzrle 1
set after_bulk 0

set timeout 200
set ret 0

set cmd_file [lindex $argv 0]
set user [lindex $argv 1]
set passwd [lindex $argv 2]
set fd [open $cmd_file r]
set tmp [read $fd]
set tmp [string trimright $tmp \n]
close $fd

#set mon_addr [lindex $argv 1]
#set mon_port [lindex $argv 2]
#
#set mig_prot [lindex $argv 3]
#set mig_addr [lindex $argv 4]
#set mig_port [lindex $argv 5]

set source_cmd [format %s%s ${tmp} " -monitor telnet:127.0.0.1:55555,server,nowait"]
send_user "$source_cmd\n"
spawn bash -c $source_cmd
set source $spawn_id

set dest_cmd [format %s%s%s ${tmp} " -monitor telnet:127.0.0.1:55556,server,nowait" " -incoming tcp:0:4444"]
send_user "$dest_cmd\n"
spawn bash -c $dest_cmd 
set dest $spawn_id

expect {
	-i $source timeout {
		send_user "\nError: bootup timeout\n"
		exit -1
	}
	-i $source "login:" {
		send -i $source "$user\r"
		expect -i $source "Password:"
		send -i $source "$passwd\r"
	}
}

#interact -i $source
if {$has_workload == 1} {
	send -i $source "cd git/linux\r"
	send -i $source "make clean && make -j4 mm/\r"
	send -i $source "ls mm/built-in.a\r"
	send -i $source "~/check_result.sh $?\r"
}

# wait for make clean a while
sleep 10

spawn telnet localhost 55555
set source_telnet $spawn_id
spawn telnet localhost 55556
set dest_telnet $spawn_id

#The script expects
expect -i $source_telnet -nocase "qemu"

send_user "start migration \n"

# setup capability for both side
if {$use_multifd == 1} {
	send -i $source_telnet "migrate_set_capability multifd on\r"
	send -i $source_telnet "migrate_set_parameter multifd-channels 2\r"
	send -i $dest_telnet "migrate_set_capability multifd on\r"
	send -i $dest_telnet "migrate_set_parameter multifd-channels 2\r"
}

#send -i $source_telnet "migrate_set_speed 0\r"

if {$use_xbzrle == 1} {
	send -i $source_telnet "migrate_set_capability xbzrle on\r"
	send -i $dest_telnet "migrate_set_capability xbzrle on\r"
}

if {$use_compress == 1} {
	send -i $source_telnet "migrate_set_capability compress on\r"
	send -i $dest_telnet "migrate_set_capability compress on\r"
	send -i $source_telnet "migrate_set_parameter compress-threads 1\r"
	send -i $dest_telnet "migrate_set_parameter compress-threads 1\r"
}

if {$use_postcopy == 1} {
	send -i $source_telnet "migrate_set_capability postcopy-ram on\r"
	send -i $source_telnet "migrate_set_capability postcopy-blocktime on\r"
	send -i $dest_telnet "migrate_set_capability postcopy-ram on\r"
	send -i $dest_telnet "migrate_set_capability postcopy-blocktime on\r"
}

send -i $source_telnet "migrate -d tcp:0:4444\r"
sleep 5
send -i $source_telnet "info migrate\r"

# check dirty sync count
expect {
	-i $source_telnet -indices -re {dirty sync count: *([0-9]{1,4})} {
		set sync_count $expect_out(1,string)
		puts -nonewline "\n### current dirty sync count :"
		puts $sync_count
		sleep 3
		send -i $source_telnet "info migrate\r"
		if {$sync_count <= 1 && $after_bulk == 1} {
			exp_continue
		} else {
			if {$use_postcopy == 1} {
				send -i $source_telnet "migrate_start_postcopy\r"
			}
		}
	}
	-i $source_telnet eof {
		send_user "\nsource vm terminated unexpectedly!\n"
		exit -1
	}
	-i $source_telnet "Migration status: failed" {
		send_user "\nsource vm migration failed!\n"
		exit -1
	}
}

expect {
	-i $source_telnet "Migration status: active" {
		send -i $source_telnet "info migrate\r"
		sleep 10
		exp_continue
	}
	-i $source_telnet "Migration status: completed" {}
	-i $source_telnet "Migration status: failed" {
		send_user "\nsource vm migration failed!\n"
		exit -1
	}
	-i $source_telnet eof {
		send_user "\nsource vm terminated unexpectedly!\n"
		exit -1
	}
}

send_user "migration done on source\n"
if {$has_workload == 1} {
	expect {
		-i $dest "Build succeed" {
			send_user "\nLooks good!\n"
		}
		-i $dest "Build failed" {
			set ret -1
			send_user "\nSomething wrong!\n"
		}
		-i $dest timeout {
			send_user "\nError: destination no response!\n"
			exit -1
		}
	}
}

#send -i $dest "ls /etc/hosts\r"
send -i $dest "shutdown now\r"
expect {
	-i $dest eof {
		send_user "\ndestination power off successfully!\n"
	}
	-i $dest timeout {
		send_user "\nError: destination no response!\n"
		exit -1
	}
}
exit $ret
