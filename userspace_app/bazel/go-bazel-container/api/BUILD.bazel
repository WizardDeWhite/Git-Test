load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")

## Build Binary
go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "github.com/grahambrooks/go-bazel-container/api",
    visibility = ["//visibility:public"],
    deps = [
        "@com_github_gorilla_mux//:go_default_library",
    ],
)

go_binary(
    name = "api",
    embed = [":go_default_library"],
    visibility = ["//visibility:public"],
)

load("@io_bazel_rules_docker//contrib:passwd.bzl", "passwd_entry", "passwd_file")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")

## Custom base layer

passwd_entry(
    name = "nonroot-user",
    gid = 1001,
    home = "/home/nonroot-user",
    shell = "/bin/bash",
    uid = 1001,
    username = "nonroot-user",
)

passwd_file(
    name = "passwd",
    entries = [
        ":nonroot-user",
    ],
)

pkg_tar(
    name = "passwd-tar",
    srcs = [":passwd"],
    mode = "0644",
    package_dir = "etc",
    visibility = ["//visibility:public"],
)


load("@io_bazel_rules_docker//go:image.bzl", "go_image")
load(
    "@io_bazel_rules_docker//container:container.bzl",
    "container_bundle",
    "container_image",
    "container_push",
)

## Build Image
container_image(
    name = "passwd-image",
    base = "@go_image_base//image",
    tars = [":passwd-tar"],
    user = "1001",
    visibility = ["//visibility:public"],
)

go_image(
    name = "go_image",
    base = ":passwd-image",
    embed = [":go_default_library"],
    visibility = ["//visibility:public"],
    importpath = "github.com/grahambrooks/go-bazel-container",
)

# docker run -p8080:8080 bazel/api:api-container
container_image(
    name = "api-container",
    base = ":go_image",
    ports = ["8080:8080"],
)

load("@io_bazel_rules_docker//contrib:push-all.bzl", "docker_push")

## Push Image

# push images in bundle to DockerHub
docker_push(
    name = "push-docker-image",
    bundle = ":build-docker-images",
)

# could push a list of images
# DockerHub/image_name : target_name
container_bundle(
    name = "build-docker-images",
    images = {
        # cmd images
        "index.docker.io/shyw13/api_image:v0.1": "//api:go_image",
    },
)

# alternative to docker_push()
# push an individual docker image
container_push(
    name = "push-container-image",
    format = "Docker",
    image = "//api:go_image",
    registry = "index.docker.io/shyw13",
    repository = "api_image",
    tag = "v0.2",
)
