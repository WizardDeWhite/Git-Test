
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;                              string.asm
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;                                                       Forrest Yu, 2005
; ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

[SECTION .text]

; 导出函数
global	memcpy


; ------------------------------------------------------------------------
; void* memcpy(void* es:pDest, void* ds:pSrc, int iSize);
; ------------------------------------------------------------------------
memcpy:
	push	ebp
	mov	ebp, esp

	; current stack
	;+----------------------+
	;|    ebp               |  <- esp, ebp = esp
	;+----------------------+
	;|    EIP               |
	;+----------------------+
	;|    pDest             |
	;+----------------------+
	;|    pSrc              |
	;+----------------------+
	;|    iSize             |
	;+----------------------+

	push	esi
	push	edi
	push	ecx

	; current stack
	;+----------------------+
	;|    esi               |  <- esp
	;+----------------------+
	;|    edi               |
	;+----------------------+
	;|    ecx               |
	;+----------------------+
	;|    ebp               |  <- ebp
	;+----------------------+
	;|    EIP               |
	;+----------------------+
	;|    pDest             |
	;+----------------------+
	;|    pSrc              |
	;+----------------------+
	;|    iSize             |
	;+----------------------+

	mov	edi, [ebp + 8]	; Destination
	mov	esi, [ebp + 12]	; Source
	mov	ecx, [ebp + 16]	; Counter
.1:
	cmp	ecx, 0		; 判断计数器
	jz	.2		; 计数器为零时跳出

	mov	al, [ds:esi]		; ┓
	inc	esi			; ┃
					; ┣ 逐字节移动
	mov	byte [es:edi], al	; ┃
	inc	edi			; ┛

	dec	ecx		; 计数器减一
	jmp	.1		; 循环
.2:
	mov	eax, [ebp + 8]	; 返回值

	pop	ecx
	pop	edi
	pop	esi
	mov	esp, ebp
	pop	ebp

	ret			; 函数结束，返回
; memcpy 结束-------------------------------------------------------------
